/-
- Created in 2025 by GaÃ«tan SerrÃ©
-/

import ConsistencyGO.Algorithm
import ConsistencyGO.Utils.Compact
import ConsistencyGO.Utils.Tendsto
import Mathlib.Analysis.Normed.Order.Lattice

variable {Î± : Type*} [PseudoMetricSpace Î±]

/-- Given a sequence `u` and a element `x`, returns `min_(0 â‰¤ i < n) dist (u i) x. -/
noncomputable def min_dist_x :=
  fun {n : â„•} (u : Fin n â†’ Î±) (x : Î±) => Tuple.min ((fun xi => dist xi x) âˆ˜ u)

/-- `min_dist_x` is continuous -/
lemma min_dist_x_continuous {n : â„•} (u : Fin n â†’ Î±) : Continuous (min_dist_x u) := by
  by_cases h : n = 0
  Â· have empty : Â¬Nonempty (Fin n) := by
      rw [â†Fin.pos_iff_nonempty]
      exact Eq.not_gt h
    let e : â„ := instNonemptyOfInhabited.some
    suffices h : min_dist_x u = fun x => e by rw [h]; exact continuous_const
    ext x
    unfold min_dist_x Tuple.min Fintype.min_image Fintype.min_image'
    split
    Â· contradiction
    rfl

  have ne_fin : Nonempty (Fin n) := Fin.pos_iff_nonempty.mp (Nat.zero_lt_of_ne_zero h)
  have ne : (Finset.univ : Finset (Fin n)).Nonempty :=
    Finset.univ_nonempty_iff.mpr ne_fin

  set g := fun (i : Fin n) (x : Î±) => dist (u i) x

  have continuous_inf_g : Continuous (Finset.univ.inf' ne g) := by
    suffices h : âˆ€ i âˆˆ Finset.univ, Continuous (g i) from Continuous.finset_inf' ne h
    intro i _
    exact Continuous.dist continuous_const continuous_id

  suffices h : min_dist_x u = Finset.univ.inf' ne g by rwa [h]
  ext x
  unfold min_dist_x Tuple.min Fintype.min_image Fintype.min_image'
  split
  Â· simp only [Function.comp_apply, Finset.inf'_apply, g]
  contradiction

variable [CompactSpace Î±] [Nonempty Î±]
variable {Î² : Type*} [Nonempty Î²] [PseudoMetricSpace Î²] [LinearOrder Î²] [ClosedIciTopology Î²]
[ClosedIicTopology Î²]

/-- The maximum of a continuous function over `Î±`. -/
noncomputable def fmax {f : Î± â†’ Î²} (hf : Continuous f) := f (compact_argmax hf)
noncomputable def fmin {f : Î± â†’ Î²} (hf : Continuous f) := f (compact_argmin hf)

variable [MeasurableSpace Î±]

/-- Given an algorithm `A`, the function that, given `Îµ` and `n`, returns
the measure of the set of sequences of size `n` such that the maximum of
`f` over these sequences is at least `Îµ` away from from `fmax`. -/
def measure_dist_max (A : Algorithm Î± Î²) {f : Î± â†’ Î²} (hf : Continuous f) :=
  fun Îµ n => A.Î¼ f n {u | dist (Tuple.max (f âˆ˜ u)) (fmax hf) > Îµ}


open Filter Topology
/-- **Main definition**: An algorithm `A` is consistent over a continuous function `f`
if for any `Îµ > 0`, `lim_(n â†’ âˆ) measure_dist_max n = 0`. -/
def isConsistent (A : Algorithm Î± Î²) {f : Î± â†’ Î²} (hf : Continuous f) : Prop :=
  âˆ€ â¦ƒÎµâ¦„, 0 < Îµ â†’ Tendsto (measure_dist_max A hf Îµ) atTop (ğ“ 0)

/-- An algorithm `A` is consistent over all continuous functions. -/
def isConsistentOverContinuous (A : Algorithm Î± Î²) {f : Î± â†’ Î²} (hf : Continuous f) : Prop :=
  isConsistent A hf

/-- Given a sequence `u`, maximum over `Î±` of `min_dist_x u`: the maximum distance between
any element in `Î±` and `u`. -/
noncomputable def max_min_dist {n : â„•} (u : Fin n â†’ Î±) :=
  min_dist_x u (compact_argmax (min_dist_x_continuous u))

/-- **Main definition**: Given a function `f`, an algorithm `A` sample the whole space
if `âˆ€ Îµ > 0, lim_(n â†’ âˆ) A.Î¼ f n {u | max_min_dist u > Îµ} = 0`. -/
noncomputable def sample_whole_space (A : Algorithm Î± Î²) (f : Î± â†’ Î²) : Prop :=
  âˆ€ Îµ > 0, Tendsto (fun n => A.Î¼ f n {u | max_min_dist u > Îµ}) atTop (ğ“ 0)

lemma Îµ_cover_ne {Îµ : â„} (hÎµ : Îµ > 0) (Î± : Type*) [PseudoMetricSpace Î±]
    [Nonempty Î±] [CompactSpace Î±] :
    {n : nstar | âˆƒ (t : Finset Î±), t.card = n.1 âˆ§ Set.univ = â‹ƒ x âˆˆ t, Metric.ball x Îµ}.Nonempty
    := by
  let U := fun (x : Î±) => Metric.ball x Îµ
  have hU : âˆ€ (x : Î±), U x âˆˆ nhds x := fun x => Metric.ball_mem_nhds x hÎµ
  obtain âŸ¨t, htâŸ© := finite_cover_nhds hU
  refine âŸ¨âŸ¨t.card, ?_âŸ©, t, rfl, ht.symmâŸ©
  by_contra h_contra
  have union_is_empty : â‹ƒ x âˆˆ t, U x = âˆ… := by
      rw [Finset.card_eq_zero.mp (Nat.eq_zero_of_le_zero <| Nat.le_of_not_lt h_contra)]
      simp only [Finset.notMem_empty, Set.iUnion_of_empty, Set.iUnion_empty]
  rw [union_is_empty] at ht
  exact Set.empty_ne_univ ht
